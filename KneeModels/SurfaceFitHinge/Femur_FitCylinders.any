AnyFunTransform3D &CT2scaledSeg = SubjectSpecificScaling.CT2scaledSeg;
AnyFunTransform3D &Femur2CT = SubjectSpecificScaling.scaledSeg2CT;
//------------------------------------------------------------------------------
// Surface fits for entire lateral and medial condyles, for new hinge joint model

AnyDrawSurf Medial_TF_surf = {
  FileName = "STLs\Medial_TF.stl";
  Visible=Off;
  ScaleXYZ = {1.0, 1.0, 1.0}*..AMirroring;
  RGB = {0,1,0};
  AnyFunTransform3D &Scale =.CT2scaledSeg;
  Face=-1;
  AnyInt NumPoints = STL_Size(FileName, 1)[0];
  AnyFloat points = GetVertices(iarr(0, 1, NumPoints-1));
};

AnyDrawSurf Lateral_TF_surf = {
  FileName = "STLs\Lateral_TF.stl";
  Visible=Off;
  ScaleXYZ = {1.0, 1.0, 1.0}*..AMirroring;
  RGB = {0,1,0};
  AnyFunTransform3D &Scale =.CT2scaledSeg;
  Face=-1;
  AnyInt NumPoints = STL_Size(FileName, 1)[0];
  AnyFloat points = GetVertices(iarr(0, 1, NumPoints-1));
};

AnyFloat TF_points =arrcat(Medial_TF_surf.points, Lateral_TF_surf.points);
FitCylinder(TF)
FitCylinder_PC(TF,Medial_TF,On,On)
FitCylinder_PC(TF,Lateral_TF,On,On)

AddPointCloud(Medial_TF,1,0)
AddPointCloud(Lateral_TF,1,0)

AnyFolder CT_Data={
  AnyVec3 Medial_TF = .Femur2CT(.Medial_TF.sRel);
  AnyVec3 Lateral_TF = .Femur2CT(.Lateral_TF.sRel);
  AnyVec3 MedialEpicondyle= .Femur2CT(.EpicondylusFemorisMedialis.sRel);
  AnyVec3 LateralEpicondyle= .Femur2CT(.EpicondylusFemorisLateralis.sRel);
  AnyVec3 HipJointCenter= .Femur2CT(.HipJoint.sRel);
};

AnyVec3 rOffset= KneeJointAnatomicalFrame.sRel;
AnyMat33 AOffset= KneeJointAnatomicalFrame.ARel;

AnyRefNode KneeJoint = {
  sRel = (.EpicondylusFemorisMedialis.sRel+.EpicondylusFemorisLateralis.sRel)/2.0;
  ARel = RotMat(sRel,.EpicondylusFemorisLateralis.sRel,.HipJoint.sRel)*RotMat(...Sign*pi/2,y);      
  AnyDrawRefFrame drw={Visible=Off;ScaleXYZ=0.1*{1,1,1};RGB={0,1,0};}; 
};

KneeJointAnatomicalFrame = {
  AnyVec3 rOffset= sRel;
  AnyMat33 AOffset= ARel;
  
  AnyRefNode Lateral_TF = {
    DEFINE_LOCAL_COORDINATE(..Lateral_TF.sRel)
    AnyDrawNode drw={Visible=Off;RGB={0,0,1};ScaleXYZ={1,1,1}*0.002;};
  };
  AnyRefNode Medial_TF = {
    DEFINE_LOCAL_COORDINATE(..Medial_TF.sRel)
    AnyDrawNode drw={Visible=Off;RGB={0,0,1};ScaleXYZ={1,1,1}*0.002;};
  }; 
  AnyRefNode HipJoint = {
    DEFINE_LOCAL_COORDINATE(..HipJoint.sRel)
    AnyDrawNode drw={Visible=Off;RGB={0,0,1};ScaleXYZ={1,1,1}*0.002;};
  };
  
  AnyRefNode KneeJoint = {
    AnyVec3 O = (.Medial_TF.sRel + .Lateral_TF.sRel)/2; 
    AnyVec3 Z = ....Sign*(.Lateral_TF.sRel - .Medial_TF.sRel);
    AnyVec3 X = cross((.HipJoint.sRel-O),Z);
    AnyVec3 Y = cross(Z,X);
    sRel= O;
    ARel ={X/vnorm(X),Y/vnorm(Y),Z/vnorm(Z)}';
    AnyDrawRefFrame drw={Visible= On; ScaleXYZ=0.1*{1,1,1};RGB={0,0,1};};
  };
};